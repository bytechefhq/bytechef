plugins {
    alias(libs.plugins.org.sonarqube)
    alias(libs.plugins.org.springframework.boot) apply false
}

file("${projectDir}/sonar-project.properties").withReader {
    Properties sonarProperties = new Properties()
    sonarProperties.load(it)

    sonarProperties.each { key, value ->
        sonar {
            properties {
                property key, value
            }
        }
    }
}

subprojects {
    apply plugin: "io.spring.dependency-management"
    apply plugin: "org.sonarqube"

    apply from: "${rootDir}/gradle/sonar.gradle"

    dependencyManagement {
        imports {
            mavenBom "org.springframework.cloud:spring-cloud-dependencies:${libs.versions.org.springframework.cloud.dependencies.get()}"
        }
    }

    dependencies {
        implementation platform(org.springframework.boot.gradle.plugin.SpringBootPlugin.BOM_COORDINATES)

        testImplementation "org.springframework.boot:spring-boot-starter-test"

        testRuntimeOnly "com.zaxxer:HikariCP"
        testRuntimeOnly "org.postgresql:postgresql"
        testRuntimeOnly libs.testcontainers.postgresql
    }
}

plugins {
    id "com.diffplug.spotless"
    id "com.github.ben-manes.versions"
    id "com.github.spotbugs"
    id "com.integri.java-common-conventions"
    id "io.spring.nohttp"
}

allprojects {
    apply plugin: "com.github.ben-manes.versions"
}

subprojects {
    apply plugin: "com.diffplug.spotless"
    apply plugin: "com.github.spotbugs"
    apply plugin: "io.spring.nohttp"

    eclipse {
        sourceSets {
            main {
                java {
                    srcDirs += ["build/generated/sources/annotationProcessor/java/main"]
                }
            }
        }
    }

    idea {
        module {
            excludeDirs += files("node_modules")
        }
    }

    checkstyle {
        toolVersion "${checkstyleVersion}"
        configFile file("checkstyle.xml")
        checkstyleTest.enabled = false
    }

    nohttp {
        source.include "build.gradle", "README.md"
    }

    spotless {
        format "misc", {
            // define the files to apply `misc` to
            target "*.gradle", "src/*/java/**/*.java", "*.md", ".gitignore"

            trimTrailingWhitespace()
            indentWithSpaces(4)
            endWithNewline()
        }
        java {
            target "src/*/java/**/*.java"

            importOrder()
            licenseHeader(
                '''
                /*
                 * Copyright 2021 the original author or authors.
                 *
                 * Licensed under the Apache License, Version 2.0 (the "License");
                 * you may not use this file except in compliance with the License.
                 * You may obtain a copy of the License at
                 *
                 *      http://www.apache.org/licenses/LICENSE-2.0
                 *
                 * Unless required by applicable law or agreed to in writing, software
                 * distributed under the License is distributed on an "AS IS" BASIS,
                 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
                 * See the License for the specific language governing permissions and
                 * limitations under the License.
                 *
                 * Modifications copyright (C) 2021 <your company/name>
                 */
                '''
            ).named(
                "standard"
            )
            licenseHeader(
                '''
                /*
                 * Copyright 2016-2018 the original author or authors.
                 *
                 * Licensed under the Apache License, Version 2.0 (the "License");
                 * you may not use this file except in compliance with the License.
                 * You may obtain a copy of the License at
                 *
                 *      http://www.apache.org/licenses/LICENSE-2.0
                 *
                 * Unless required by applicable law or agreed to in writing, software
                 * distributed under the License is distributed on an "AS IS" BASIS,
                 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
                 * See the License for the specific language governing permissions and
                 * limitations under the License.
                 */
                '''
            ).named(
                "original"
            ).onlyIfContentMatches(
                "author\\sArik\\sCohen"
            )
            prettier(["prettier": "2.4.0", "prettier-plugin-java": "1.4.0"]).config(["parser": "java", "printWidth": 120, "tabWidth": 4, "useTabs": false])
        }
    }

    task cleanResources(type: Delete) {
        delete "build/resources"
    }
}

wrapper {
    gradleVersion = "7.2.0"
}

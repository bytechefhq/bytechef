plugins {
    alias(libs.plugins.org.openapi.generator)
}

task generateOpenAPISpring(type: org.openapitools.generator.gradle.plugin.tasks.GenerateTask) {
    apiPackage = "com.bytechef.dione.integration.web.rest"
    configOptions = [
        "dateLibrary": "java8-localdatetime",
        "interfaceOnly": "true",
        "reactive": "true",
        "useSpringBoot3": "true"
    ]
    generatorName = "spring"
    importMappings = [
        CategoryModel: "com.bytechef.category.web.rest.model.CategoryModel",
        TagModel: "com.bytechef.tag.web.rest.model.TagModel",
        WorkflowModel: "com.bytechef.hermes.workflow.web.rest.model.WorkflowModel",
        WorkflowFormatModel: "com.bytechef.hermes.workflow.web.rest.model.WorkflowFormatModel",
        WorkflowTaskModel: "com.bytechef.hermes.workflow.web.rest.model.WorkflowTaskModel"
    ]
    inputSpec = "$projectDir/openapi.yaml".toString()
    modelNameSuffix = "Model"
    modelPackage = "com.bytechef.dione.integration.web.rest.model"
    outputDir = "$projectDir/generated".toString()
    schemaMappings = [
        Category: "CategoryModel",
        Tag: "TagModel",
        Workflow: "WorkflowModel",
        WorkflowFormat: "WorkflowFormatModel",
        WorkflowTask: "WorkflowTaskModel"
    ]
}

sourceSets.main.java.srcDir "${projectDir}/generated/src/main/java"

task generateOpenAPITypeScriptFetch(type: org.openapitools.generator.gradle.plugin.tasks.GenerateTask) {
    generatorName = "typescript-fetch"
    inputSpec = "$projectDir/openapi.yaml".toString()
    modelNameSuffix = "Model"
    outputDir = "$rootDir/client/src/middleware/embedded/integration".toString()
    typeMappings = [
        DateTime: "Date"
    ]
}

tasks.register("generateOpenAPI") {
    dependsOn tasks.generateOpenAPISpring, tasks.generateOpenAPITypeScriptFetch
}

dependencies {
    annotationProcessor libs.org.mapstruct.mapstruct.processor
    annotationProcessor libs.org.mapstruct.extensions.spring.mapstruct.spring.extensions

    api project(":server:libs:hermes:hermes-workflow:hermes-workflow-rest:hermes-workflow-rest-api")
    api project(":server:libs:core:category:category-rest:category-rest-api")
    api project(":server:libs:core:tag:tag-rest:tag-rest-api")

    implementation libs.org.openapitools.jackson.databind.nullable
    implementation "jakarta.annotation:jakarta.annotation-api"
    implementation "jakarta.validation:jakarta.validation-api"
    implementation libs.org.mapstruct
    implementation libs.org.mapstruct.extensions.spring.mapstruct.spring.annotations
    implementation "org.slf4j:slf4j-api"
    implementation "org.springframework:spring-context"
    implementation "org.springframework:spring-web"
    implementation libs.io.swagger.core.v3.swagger.annotations
    implementation project(":server:ee:libs:dione:dione-integration:dione-integration-api")

    testImplementation "org.springframework.boot:spring-boot-starter-web"
}

plugins {
    alias(libs.plugins.org.openapi.generator)
}

task generateOpenAPISpring(type: org.openapitools.generator.gradle.plugin.tasks.GenerateTask) {
    apiPackage = "com.bytechef.hermes.configuration.web.rest"
    configOptions = [
        "dateLibrary": "java8-localdatetime",
        "interfaceOnly": "true",
        "useSpringBoot3": "true"
    ]
    generatorName = "spring"
    inputSpec = "$projectDir/openapi.yaml".toString()
    modelNameSuffix = "Model"
    modelPackage = "com.bytechef.hermes.configuration.web.rest.model"
    outputDir = "$projectDir/generated".toString()
    schemaMappings = [
        "Page": "org.springframework.data.domain.Page"
    ]
}

sourceSets.main.java.srcDir "${projectDir}/generated/src/main/java"

task generateOpenAPITypeScriptFetch(type: org.openapitools.generator.gradle.plugin.tasks.GenerateTask) {
    generatorName = "typescript-fetch"
    inputSpec = "$projectDir/openapi.yaml".toString()
    modelNameSuffix = "Model"
    outputDir = "$rootDir/client/src/middleware/hermes/configuration".toString()
    typeMappings = [
        DateTime: "Date"
    ]
}

tasks.register("generateOpenAPI") {
    dependsOn tasks.generateOpenAPISpring, tasks.generateOpenAPITypeScriptFetch
}

dependencies {
    annotationProcessor libs.org.mapstruct.mapstruct.processor
    annotationProcessor libs.org.mapstruct.extensions.spring.mapstruct.spring.extensions

    compileOnly "jakarta.servlet:jakarta.servlet-api:6.0.0"

    implementation libs.io.swagger.core.v3.swagger.annotations
    implementation libs.org.openapitools.jackson.databind.nullable
    implementation "jakarta.annotation:jakarta.annotation-api"
    implementation "jakarta.validation:jakarta.validation-api"
    implementation libs.org.mapstruct
    implementation libs.org.mapstruct.extensions.spring.mapstruct.spring.annotations
    implementation "org.slf4j:slf4j-api"
    implementation "org.springframework:spring-context"
    implementation "org.springframework:spring-web"
    implementation project(":server:libs:core:autoconfigure-annotations")
    implementation project(":server:libs:core:commons:commons-util")
    implementation project(":server:libs:hermes:hermes-configuration:hermes-configuration-api")
    implementation project(":server:libs:hermes:hermes-test:hermes-test-api")
}

plugins {
    alias(libs.plugins.org.openapi.generator)
}

task generateOpenAPISpring(type: org.openapitools.generator.gradle.plugin.tasks.GenerateTask) {
    apiPackage = "com.bytechef.hermes.workflow.web.rest"
    configOptions = [
        "dateLibrary": "java8-localdatetime",
        "interfaceOnly": "true",

        "useSpringBoot3": "true"
    ]
    generatorName = "spring"
    importMappings = [
        "Page": "org.springframework.data.domain.Page",
    ]
    inputSpec = "$projectDir/openapi.yaml".toString()
    modelNameSuffix = "Model"
    modelPackage = "com.bytechef.hermes.workflow.web.rest.model"
    outputDir = "$projectDir/../hermes-workflow-rest-api/generated".toString()
    schemaMappings = [
        "Page": "org.springframework.data.domain.Page"
    ]
}

task generateOpenAPITypeScriptFetch(type: org.openapitools.generator.gradle.plugin.tasks.GenerateTask) {
    generatorName = "typescript-fetch"
    inputSpec = "$projectDir/openapi.yaml".toString()
    modelNameSuffix = "Model"
    outputDir = "$rootDir/client/src/middleware/core/workflow".toString()
    typeMappings = [
        DateTime: "Date"
    ]
}

tasks.register("generateOpenAPI") {
    dependsOn tasks.generateOpenAPISpring, tasks.generateOpenAPITypeScriptFetch
}

dependencies {
    api project(":server:libs:core:message-broker:message-broker-api")
    api project(":server:libs:hermes:hermes-workflow:hermes-workflow-rest:hermes-workflow-rest-api")

    implementation "org.springframework:spring-context"
    implementation "org.springframework:spring-web"
    implementation project(":server:libs:core:commons:commons-util")

    testImplementation "org.springframework.boot:spring-boot-starter-web"
}

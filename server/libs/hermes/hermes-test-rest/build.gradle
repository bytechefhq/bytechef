plugins {
    alias(libs.plugins.org.openapi.generator)
}

task generateOpenAPISpring(type: org.openapitools.generator.gradle.plugin.tasks.GenerateTask) {
    apiPackage = "com.bytechef.hermes.test.web.rest"
    configOptions = [
        "dateLibrary": "java8-localdatetime",
        "interfaceOnly": "true",
        "useSpringBoot3": "true"
    ]
    generatorName = "spring"
    importMappings = [
        "TaskExecutionModel": "com.bytechef.hermes.execution.web.rest.model.TaskExecutionModel"
    ]
    inputSpec = "$projectDir/openapi.yaml".toString()
    modelNameSuffix = "Model"
    modelPackage = "com.bytechef.hermes.test.web.rest.model"
    outputDir = "$projectDir/generated".toString()
    schemaMappings = [
        "Page": "org.springframework.data.domain.Page",
        ComponentDefinition: "com.bytechef.hermes.execution.web.rest.model.ComponentDefinitionModel",
        ExecutionError: "com.bytechef.hermes.execution.web.rest.model.ExecutionErrorModel",
        Job: "com.bytechef.hermes.execution.web.rest.model.JobModel",
        JobBasic: "com.bytechef.hermes.execution.web.rest.model.JobBasicModel",
        JobConnection: "com.bytechef.hermes.execution.web.rest.model.JobConnectionModel",
        JobParameters: "com.bytechef.hermes.execution.web.rest.model.JobParametersModel",
        Page: "org.springframework.data.domain.Page",
        Tag: "com.bytechef.tag.web.rest.model.TagModel",
        TaskExecution: "TaskExecutionModel",
        Webhook: "com.bytechef.hermes.execution.web.rest.model.WebhookModel",
        Webhook_retry: "com.bytechef.hermes.execution.web.rest.model.WebhookRetryModel",
        Workflow: "com.bytechef.hermes.configuration.web.rest.model.WorkflowModel",
        WorkflowFormat: "com.bytechef.hermes.configuration.web.rest.model.WorkflowFormatModel",
        WorkflowTask: "com.bytechef.hermes.configuration.web.rest.model.WorkflowTaskModel"
    ]
}

task generateOpenAPITypeScriptFetch(type: org.openapitools.generator.gradle.plugin.tasks.GenerateTask) {
    generatorName = "typescript-fetch"
    inputSpec = "$projectDir/openapi.yaml".toString()
    modelNameSuffix = "Model"
    outputDir = "$rootDir/client/src/middleware/core/workflow/test".toString()
    typeMappings = [
        DateTime: "Date"
    ]
}

tasks.register("generateOpenAPI") {
    dependsOn tasks.generateOpenAPISpring, tasks.generateOpenAPITypeScriptFetch
}

sourceSets.main.java.srcDir "${projectDir}/generated/src/main/java"

dependencies {
    annotationProcessor libs.org.mapstruct.mapstruct.processor
    annotationProcessor libs.org.mapstruct.extensions.spring.mapstruct.spring.extensions

    api "org.springframework.data:spring-data-commons"
    api project(":server:libs:hermes:hermes-execution:hermes-execution-rest:hermes-execution-rest-api")

    compileOnly "jakarta.servlet:jakarta.servlet-api:6.0.0"

    implementation libs.io.swagger.core.v3.swagger.annotations
    implementation "jakarta.annotation:jakarta.annotation-api"
    implementation "jakarta.validation:jakarta.validation-api"
    implementation libs.org.openapitools.jackson.databind.nullable
    implementation libs.org.mapstruct
    implementation libs.org.mapstruct.extensions.spring.mapstruct.spring.annotations
    implementation "org.springframework:spring-context"
    implementation "org.springframework:spring-web"
    implementation project(":server:libs:core:commons:commons-util")

    testImplementation "org.springframework:spring-webflux"
    testImplementation "org.springframework.boot:spring-boot-starter-web"
}

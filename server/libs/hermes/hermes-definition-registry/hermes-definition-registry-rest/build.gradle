plugins {
    alias(libs.plugins.org.openapi.generator)
}

task generateOpenAPISpring(type: org.openapitools.generator.gradle.plugin.tasks.GenerateTask) {
    apiPackage = "com.bytechef.hermes.definition.registry.web.rest"
    configOptions = [
        "dateLibrary": "java8-localdatetime",
        "interfaceOnly": "true",

        "useSpringBoot3": "true"
    ]
    generatorName = "spring"
    inputSpec = "$projectDir/openapi.yaml".toString()
    modelNameSuffix = "Model"
    modelPackage = "com.bytechef.hermes.definition.registry.web.rest.model"
    outputDir = "$projectDir/generated".toString()
}

sourceSets.main.java.srcDir "${projectDir}/generated/src/main/java"

task generateOpenAPITypeScriptFetch(type: org.openapitools.generator.gradle.plugin.tasks.GenerateTask) {
    generatorName = "typescript-fetch"
    inputSpec = "$projectDir/openapi.yaml".toString()
    modelNameSuffix = "Model"
    outputDir = "$rootDir/client/src/middleware/core/definition-registry".toString()
    typeMappings = [
        DateTime: "Date"
    ]
}

tasks.register("generateOpenAPI") {
    dependsOn tasks.generateOpenAPISpring, tasks.generateOpenAPITypeScriptFetch
}

dependencies {
    annotationProcessor libs.org.mapstruct.mapstruct.processor
    annotationProcessor libs.org.mapstruct.extensions.spring.mapstruct.spring.extensions

    api project(":server:libs:core:oauth2:oauth2-api")
    api project(":server:libs:hermes:hermes-connection:hermes-connection-api")
    api project(":server:libs:hermes:hermes-definition-registry:hermes-definition-registry-api")

    compileOnly "jakarta.servlet:jakarta.servlet-api:6.0.0"

    implementation libs.org.openapitools.jackson.databind.nullable
    implementation "com.fasterxml.jackson.core:jackson-databind"
    implementation "jakarta.annotation:jakarta.annotation-api"
    implementation "jakarta.validation:jakarta.validation-api"
    implementation libs.org.mapstruct
    implementation libs.org.mapstruct.extensions.spring.mapstruct.spring.annotations
    implementation "org.slf4j:slf4j-api"
    implementation "org.springframework:spring-context"
    implementation "org.springframework:spring-web"
    implementation libs.io.swagger.core.v3.swagger.annotations
    implementation project(":server:libs:core:commons:commons-util")

    testImplementation "org.springframework.boot:spring-boot-starter-web"
}

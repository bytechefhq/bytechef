plugins {
    alias(libs.plugins.org.openapi.generator)
}

task generateOpenAPISpring(type: org.openapitools.generator.gradle.plugin.tasks.GenerateTask) {
    apiPackage = "com.bytechef.atlas.web.rest"
    configOptions = [
        "dateLibrary": "java8-localdatetime",
        "interfaceOnly": "true",
        "reactive": "true",
        "useSpringBoot3": "true"
    ]
    generatorName = "spring"
    importMappings = [
        "Page": "org.springframework.data.domain.Page",
    ]
    inputSpec = "$projectDir/openapi.yaml".toString()
    modelNameSuffix = "Model"
    modelPackage = "com.bytechef.atlas.web.rest.model"
    outputDir = "$projectDir/../atlas-rest-api/generated".toString()
    schemaMappings = [
        "Page": "org.springframework.data.domain.Page"
    ]
}

task generateOpenAPITypeScriptFetch(type: org.openapitools.generator.gradle.plugin.tasks.GenerateTask) {
    generatorName = "typescript-fetch"
    inputSpec = "$projectDir/openapi.yaml".toString()
    modelNameSuffix = "Model"
    outputDir = "$rootDir/client/src/middleware/workflow".toString()
    typeMappings = [
        DateTime: "Date"
    ]
}

tasks.register("generateOpenAPI") {
    dependsOn tasks.generateOpenAPISpring, tasks.generateOpenAPITypeScriptFetch
}

dependencies {
    api project(":server:libs:atlas:atlas-rest:atlas-rest-api")

    implementation "org.springframework:spring-context"
    implementation "org.springframework:spring-webflux"
    implementation project(":server:libs:atlas:atlas-message-broker:atlas-message-broker-api")
    implementation project(":server:libs:core:autoconfigure-annotations")

    testImplementation "org.springframework.boot:spring-boot-starter-webflux"
}
